=head1 NAME

Search::Kino03::KinoSearch::Docs::Tutorial::BeyondSimple - A more flexible app structure.

=head1 DESCRIPTION

=head2 Goal

In this tutorial chapter, we'll refactor the apps we built in
L<Search::Kino03::KinoSearch::Docs::Tutorial::Simple> so that they look exactly the same from
the end user's point of view, but offer the developer greater possibilites for
expansion.  

To achieve this, we'll ditch Search::Kino03::KSx::Simple and replace it with the
classes that it uses internally:

=over

=item *

L<Search::Kino03::KinoSearch::Schema> - Plan out your index.

=item *

L<Search::Kino03::KinoSearch::FieldType::FullTextType> - Field type for full text search.

=item *

L<Search::Kino03::KinoSearch::Analysis::PolyAnalyzer> - A one-size-fits-all parser/tokenizer.

=item *

L<Search::Kino03::KinoSearch::Indexer> - Manipulate index content.

=item *

L<Search::Kino03::KinoSearch::Searcher> - Search an index.

=item *

L<Search::Kino03::KinoSearch::Search::Hits> - Iterate over hits returned by a Searcher.

=back

=head2 Adaptations to indexer.pl

After we load our modules...

    use Search::Kino03::KinoSearch::Schema;
    use Search::Kino03::KinoSearch::FieldType::FullTextType;
    use Search::Kino03::KinoSearch::Analysis::PolyAnalyzer;
    use Search::Kino03::KinoSearch::Indexer;

... the first item we're going need is a L<Schema|Search::Kino03::KinoSearch::Schema>. 

A Schema is analogous to an SQL table definition.  It instructs other entities
on how they should interpret the raw data in an inverted index and interact
with it.  

The primary job of a Schema is to specify what fields are available and how
they're defined.  We'll start off with three fields: title, content and url.

    # Create Schema.
    my $schema = Search::Kino03::KinoSearch::Schema->new;
    my $polyanalyzer = Search::Kino03::KinoSearch::Analysis::PolyAnalyzer->new(
        language => 'en',
    );
    my $type = Search::Kino03::KinoSearch::FieldType::FullTextType->new(
        analyzer => $polyanalyzer,
    );
    $schema->spec_field( name => 'title',   type => $type );
    $schema->spec_field( name => 'content', type => $type );
    $schema->spec_field( name => 'url',     type => $type );

All of the fields are spec'd out using the "FullTextType" FieldType,
indicating that they will be searchable as "full text" -- which means that
they can be searched for individual words.  The "analyzer", which is unique to
FullTextType fields, is what breaks up the text into searchable tokens.

Next, we'll swap our Search::Kino03::KSx::Simple object out for a Search::Kino03::KinoSearch::Indexer.  The
substitution will be straightforward because Simple has merely been serving as
a thin wrapper around an inner Indexer, and we'll just be peeling away the
wrapper.

First, replace the constructor:

    # Create Indexer.
    my $indexer = Search::Kino03::KinoSearch::Indexer->new(
        index    => $path_to_index,
        schema   => $schema,
        create   => 1,
        truncate => 1,
    );

Next, have the C<$indexer> object C<add_doc> where we were having the
C<$simple> object C<add_doc> before:

    foreach my $filename (@filenames) {
        my $doc = slurp_and_parse_file($filename);
        $indexer->add_doc($doc);
    }

There's only one extra step required: at the end of the app, you must call
commit() explicitly to close the indexing session and commit your changes.
(Search::Kino03::KSx::Simple hides this detail, calling commit() implicitly when it needs to).

    $indexer->commit;

=head2 Adaptations to search.cgi

In our search app as in our indexing app, Search::Kino03::KSx::Simple has served as a
thin wrapper -- this time around L<Search::Kino03::KinoSearch::Searcher> and
L<Search::Kino03::KinoSearch::Search::Hits>.  Swapping out Simple for these two classes is
also straightforward:

    use Search::Kino03::KinoSearch::Searcher;
    
    my $searcher = Search::Kino03::KinoSearch::Searcher->new( index => $path_to_index );
    my $hits = $searcher->hits(    # returns a Hits object, not a hit count
        query      => $q,
        offset     => $offset,
        num_wanted => $hits_per_page,
    );
    my $hit_count = $hits->total_hits;  # get the hit count here
    
    ...
    
    while ( my $hit = $hits->next ) {
        ...
    }

=head2 Hooray!

Congratulations!  Your apps do the same thing as before... but now they'll be
easier to customize.  

In our next chapter, L<Search::Kino03::KinoSearch::Docs::Tutorial::FieldType>, we'll explore
how to assign different behaviors to different fields.

=head1 COPYRIGHT

Copyright 2005-2009 Marvin Humphrey

=head1 LICENSE, DISCLAIMER, BUGS, etc.

See L<KinoSearch> version 0.30.

